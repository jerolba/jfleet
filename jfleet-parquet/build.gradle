plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'    
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    api project(':jfleet-core')

    api group: 'org.apache.parquet', name: 'parquet-common', version: parquetVersion
    api group: 'org.apache.parquet', name: 'parquet-column', version: parquetVersion
    api group: 'org.apache.parquet', name: 'parquet-hadoop', version: parquetVersion

    compileOnly ("org.apache.hadoop:hadoop-client:${hadoopVersion}")
    implementation("org.apache.hadoop:hadoop-mapreduce-client-core:${hadoopVersion}"){
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-client'
        exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-hdfs-client'
        exclude group: 'com.google.inject.extensions', module: 'guice-servlet'
        exclude group: 'io.netty', module: 'netty'
        exclude group: 'io.netty', module: 'netty-all'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'slf4j-reload4j'       
        exclude group: 'org.apache.avro', module: 'avro'   
    } 
    api("org.apache.hadoop:hadoop-common:${hadoopVersion}") {
        exclude group: 'org.apache.hadoop', module: 'hadoop-auth'        
        exclude group: 'org.apache.avro', module: 'avro'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.apache.kerby', module: 'kerb-core'
        exclude group: 'org.apache.curator', module: 'curator-recipes'
        exclude group: 'org.apache.curator', module: 'curator-client'
        exclude group: 'org.apache.hadoop', module: 'hadoop-annotations'
        exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-protobuf_3_7'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.apache.commons', module: 'commons-text'
        exclude group: 'org.apache.commons', module: 'commons-math3'
        exclude group: 'org.apache.commons', module: 'commons-configuration2'
        exclude group: 'org.apache.commons', module: 'commons-compress'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
        exclude group: 'org.eclipse.jetty', module: 'jetty-server'
        exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
        exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
        exclude group: 'org.eclipse.jetty', module: 'jetty-util'
        exclude group: 'javax.servlet', module: 'javax.servlet-api'
        exclude group: 'javax.servlet.jsp', module: 'jsp-api'
        exclude group: 'com.sun.jersey', module: 'jersey-json'
        exclude group: 'com.sun.jersey', module: 'jersey-core'
        exclude group: 'com.sun.jersey', module: 'jersey-servlet'
        exclude group: 'com.sun.jersey', module: 'jersey-server'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
        exclude group: 'commons-codec', module: 'commons-codec'
        exclude group: 'commons-io', module: 'commons-io'
        exclude group: 'commons-net', module: 'commons-net'
        exclude group: 'commons-cli', module: 'commons-cli'
        exclude group: 'commons-pool', module: 'commons-pool'
        exclude group: 'commons-logging', module: 'commons-logging'        
        exclude group: 'ch.qos.reload4j', module: 'reload4j'
        exclude group: 'org.slf4j', module: 'slf4j-reload4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'    
        exclude group: 'com.jcraft', module: 'jsch'
        exclude group: 'dnsjava', module: 'dnsjava'
        exclude group: 'com.github.pjfanning', module: 'jersey-json'
        exclude group: 'io.dropwizard.metrics', module: 'metrics-core'
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.google.re2j', module: 're2j'
        exclude group: 'org.apache.hadoop', module: 'hadoop-common'
        exclude group: 'io.netty', module: 'netty-handler'
        exclude group: 'io.netty', module: 'netty-transport-native-epoll'
        exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-protobuf_3_25'
        exclude group: 'org.codehaus.jettison', module: 'jettison'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
        exclude group: 'jakarta.activation', module: 'jakarta.activation'
        exclude group: 'jakarta.activation', module: 'jakarta.activation-api'
    }

    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'
    testRuntimeOnly group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion
    testImplementation group: 'org.apache.parquet', name: 'parquet-avro', version: parquetVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
}

configurations.implementation {
    //hadoop-mapreduce-client-core
    exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-client'
    exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-common'
    exclude group: 'org.apache.hadoop', module: 'hadoop-hdfs-client'
    exclude group: 'com.google.inject.extensions', module: 'guice-servlet'
    exclude group: 'io.netty', module: 'netty'
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.slf4j', module: 'slf4j-reload4j'  

    //hadoop-common
    //Following dependencies are needed for Path
    //exclude group: 'org.apache.commons', module: 'commons-lang3'
    //exclude group: 'commons-logging', module: 'commons-logging'
    //exclude group: 'org.apache.commons', module: 'commons-configuration2'
    //exclude group: 'org.apache.hadoop', module: 'hadoop-auth'
    exclude group: 'org.apache.kerby', module: 'kerb-core'
    exclude group: 'org.apache.curator', module: 'curator-recipes'
    exclude group: 'org.apache.curator', module: 'curator-client'
    exclude group: 'org.apache.hadoop', module: 'hadoop-annotations'
    exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-protobuf_3_7'
    exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    exclude group: 'org.apache.commons', module: 'commons-text'
    exclude group: 'org.apache.commons', module: 'commons-math3'
    exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
    exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
    exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
    exclude group: 'org.eclipse.jetty', module: 'jetty-util'
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
    exclude group: 'javax.servlet.jsp', module: 'jsp-api'
    exclude group: 'com.sun.jersey', module: 'jersey-json'
    exclude group: 'com.sun.jersey', module: 'jersey-core'
    exclude group: 'com.sun.jersey', module: 'jersey-servlet'
    exclude group: 'com.sun.jersey', module: 'jersey-server'
    exclude group: 'com.google.guava', module: 'guava'
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'commons-beanutils', module: 'commons-beanutils'
    exclude group: 'commons-codec', module: 'commons-codec'
    exclude group: 'commons-io', module: 'commons-io'
    exclude group: 'commons-net', module: 'commons-net'
    exclude group: 'commons-cli', module: 'commons-cli'
    exclude group: 'commons-pool', module: 'commons-pool'
    exclude group: 'ch.qos.reload4j', module: 'reload4j'
    exclude group: 'org.slf4j', module: 'slf4j-reload4j'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'log4j', module: 'log4j'    
    exclude group: 'com.jcraft', module: 'jsch'
    exclude group: 'dnsjava', module: 'dnsjava'
    
    //Following dependencies excluded from included hadoop-auth
    exclude group: 'org.apache.kerby', module: 'kerb-simplekdc'
    exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
    exclude group: 'net.minidev', module: 'json-smart'
    exclude group: 'org.apache.curator', module: 'curator-framework'
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("org.apache.avro:avro:1.7.7") using module("org.apache.avro:avro:1.10.2")
    }        
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
         xml.required = true
         html.required = true
    }
}

check.dependsOn jacocoTestReport

task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)

            from components.java
            artifact sourceJar
            artifact javadocJar
            version version
        }
    }
    if (project.hasProperty('sonatypeUsername')){
	    repositories {
	        maven {
	            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
	            credentials {
	                username sonatypeUsername
	                password sonatypePassword
	            }
	        }
	    }    
    } else {
		println 'No sonatype configuration found. Expected user and password at $HOME/.gradle/gradle.properties file.'    	
    }    
}

signing {
    sign publishing.publications.mavenJava
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name 'JFleet Parquet'
            description 'JFleet Parquet serialize Java classes into Parquet files'
            url 'https://github.com/jerolba/jfleet/tree/master/jfleet-parquet'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'jerolba'
                    name 'Jerónimo López'
                    email 'jerolba+jfleet@gmail.com'
                }
            }
            scm {
                connection 'scm:git:git://github.com/jerolba/jfleet.git'
                developerConnection 'scm:git:ssh://github.com:jerolba/jfleet.git'
                url 'https://github.com/jerolba/jfleet'
            }
        }
    }
}